plugins {
    id "idea"
    id "jacoco"
    id "maven-publish"
    id "org.jreleaser" version "1.19.0"
    id "org.jetbrains.kotlin.jvm" version "1.9.25"
    id "org.jetbrains.kotlin.kapt" version "1.9.25"
    id "org.jlleitschuh.gradle.ktlint" version "12.3.0"
}

ext {
    kotlinVersion = "1.9.25"
    snakeYamlVersion = "2.3"
    log4j2Version = "2.24.0"
    junitVersion = "5.11.0"
    assertjVersion = "3.26.3"
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

[compileKotlin, compileTestKotlin]*.kotlinOptions*.jvmTarget = "17"

repositories {
    mavenCentral()
}

ktlint {
    version.set("1.2.1")
}

dependencies {
    api "org.apache.logging.log4j:log4j-layout-template-json:$log4j2Version"
    api "org.yaml:snakeyaml:$snakeYamlVersion"
    kapt "org.apache.logging.log4j:log4j-core:$log4j2Version"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"

    testImplementation "org.assertj:assertj-core:$assertjVersion"
    testImplementation "org.junit.jupiter:junit-jupiter:$junitVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
}

test {
    useJUnitPlatform()
    testLogging {
        showStandardStreams = true
    }
}

jacocoTestReport {
    reports {
        xml.required = true
        html.required = false
    }
}

check.dependsOn jacocoTestReport

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            pom {
                name = "log4j2-masking-lib"
                description = "Masking library for Log4j2"
                url = "https://github.com/Romanow/log4j2-masking-lib/"
                licenses {
                    license {
                        name = "The MIT License"
                        url = "https://opensource.org/license/mit"
                    }
                }
                developers {
                    developer {
                        id = "Romanow"
                        name = "Romanov Alexey"
                        email = "romanow.tech@gmail.com"
                    }
                }
                scm {
                    url = "https://github.com/Romanow/log4j2-masking-lib/"
                    connection = "scm:git:https://github.com/Romanow/log4j2-masking-lib.git"
                    developerConnection = "scm:git:ssh://github.com/Romanow/log4j2-masking-lib.git"
                }
            }
        }
    }
    repositories {
        maven {
            url = "build/staging-deploy"
        }
    }
}

jreleaser {
    signing {
        active = "ALWAYS"
        armored = true
    }
    deploy {
        maven {
            mavenCentral {
                sonatype {
                    active = "ALWAYS"
                    url = "https://central.sonatype.com/api/v1/publisher"
                    stagingRepository("build/staging-deploy")
                }
            }
        }
    }
}
